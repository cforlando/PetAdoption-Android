apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

repositories {
    mavenCentral()
    jcenter()
    google()
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion compile_sdk.toInteger()
    buildToolsVersion build_tools

    def Properties properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())


    defaultConfig {
        applicationId "com.codefororlando.petadoption"
        minSdkVersion 21
        targetSdkVersion target_sdk.toInteger()
        versionCode version_code.toInteger()
        versionName version_name
        buildConfigField "String", "PETFINDER_API_KEY", properties.getProperty("petfinder.api.key", "")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "api"

    productFlavors {
        petAdoption {
            applicationIdSuffix ".petAdoption"
            versionNameSuffix "-petAdoption"
            dimension "api"
        }
        petFinder {
            applicationIdSuffix ".petFinder"
            versionNameSuffix "-petFinder"
            dimension "api"
        }
    }
    buildToolsVersion '26.0.2'
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    // Kotlin
    implementation kotlinLib

    // Android Support
    implementation supportAppCompat
    implementation supportGridLayout
    implementation supportRecyclerView
    implementation supportDesign
    implementation constraintLayout

    // Play Services
    implementation locationServices

    // Dagger
    implementation dagger
    annotationProcessor daggerApt
    provided jsr250

    // MVP
    implementation (nucleus) {
        exclude group: 'io.reactivex'
    }
    implementation (nucleusSupportV4) {
        exclude group: 'io.reactivex'
    }
    implementation (nucleusSupportV7) {
        exclude group: 'io.reactivex'
    }

    // Networking
    implementation picasso
    implementation retrofit
    implementation retrofitAdapter

    // RX
    implementation rxJava
    implementation rxAndroid

    // LoganSquare
    annotationProcessor loganSquareApt
    implementation loganSquare
    implementation loganSquareRetrofit

    // Logging
    implementation timber

    // Leak Canary
    debugImplementation leakCanaryDebug
//    releaseCompile leakCanaryRelease


    implementation "android.arch.persistence.room:runtime:1.0.0"
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0"

    // Misc
    implementation versions
}
